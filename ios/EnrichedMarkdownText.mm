#import "EnrichedMarkdownText.h"
#import "AccessibilityInfo.h"
#import "AttributedRenderer.h"
#import "CodeBlockBackground.h"
#import "ENRMContextMenuTextView+macOS.h"
#import "ENRMImageAttachment.h"
#import "ENRMMarkdownParser.h"
#import "ENRMTailFadeInAnimator.h"
#import "ENRMUIKit.h"
#import "EditMenuUtils.h"
#import "FontScaleObserver.h"
#import "FontUtils.h"
#import "HeightUpdateUtils.h"
#import "InlineHTMLPostProcessor.h"
#import "LastElementUtils.h"
#import "LinkTapUtils.h"
#import "MarkdownASTNode.h"
#import "MarkdownAccessibilityElementBuilder.h"
#import "MarkdownExtractor.h"
#import "ParagraphStyleUtils.h"
#import "RenderContext.h"
#import "RuntimeKeys.h"
#import "StyleConfig.h"
#import "StylePropsUtils.h"
#import "TaskListTapUtils.h"
#import "TextViewLayoutManager.h"
#import <React/RCTUtils.h>
#import <objc/runtime.h>

#import <ReactNativeEnrichedMarkdown/EnrichedMarkdownTextComponentDescriptor.h>
#import <ReactNativeEnrichedMarkdown/EventEmitters.h>
#import <ReactNativeEnrichedMarkdown/Props.h>
#import <ReactNativeEnrichedMarkdown/RCTComponentViewHelpers.h>

#include "MeasurementCache.h"

#import "RCTFabricComponentsPlugins.h"
#import <React/RCTConversions.h>
#import <React/RCTFont.h>
#import <react/utils/ManagedObjectWrapper.h>

using namespace facebook::react;

@interface EnrichedMarkdownText () <RCTEnrichedMarkdownTextViewProtocol, UITextViewDelegate>
- (void)setupTextView;
- (void)renderMarkdownContent:(NSString *)markdownString;
- (void)applyRenderedText:(NSMutableAttributedString *)attributedText;
- (void)textTapped:(ENRMTapRecognizer *)recognizer;
- (void)setupLayoutManager;
@end

@implementation EnrichedMarkdownText {
  ENRMPlatformTextView *_textView;
#if TARGET_OS_OSX
  NSScrollView *_scrollContainer;
#endif
  ENRMMarkdownParser *_parser;
  NSString *_cachedMarkdown;
  NSString *_renderedMarkdown;
  StyleConfig *_config;
  ENRMMd4cFlags *_md4cFlags;

  dispatch_queue_t _renderQueue;
  NSUInteger _currentRenderId;
  BOOL _blockAsyncRender;

  EnrichedMarkdownTextShadowNode::ConcreteState::Shared _state;
  int _heightUpdateCounter;

  FontScaleObserver *_fontScaleObserver;
  CGFloat _maxFontSizeMultiplier;

  CGFloat _lastElementMarginBottom;
  BOOL _allowTrailingMargin;
  BOOL _enableLinkPreview;
  BOOL _streamingAnimation;

  NSUInteger _previousTextLength;
  ENRMTailFadeInAnimator *_fadeAnimator;

  AccessibilityInfo *_accessibilityInfo;
#if !TARGET_OS_OSX
  NSMutableArray<UIAccessibilityElement *> *_accessibilityElements;
#else
  NSMutableArray *_accessibilityElements;
#endif
  BOOL _accessibilityNeedsRebuild;
}

+ (ComponentDescriptorProvider)componentDescriptorProvider
{
  return concreteComponentDescriptorProvider<EnrichedMarkdownTextComponentDescriptor>();
}

#pragma mark - Measuring and State

- (CGSize)measureSize:(CGFloat)maxWidth
{
  NSAttributedString *text = ENRMGetAttributedText(_textView);
  CGFloat defaultHeight = UIFontLineHeight([UIFont systemFontOfSize:16.0]);

  if (text.length == 0) {
    return CGSizeMake(maxWidth, defaultHeight);
  }

  ENRMTextLayoutResult layout = ENRMMeasureTextLayout(_textView, maxWidth);

  CGFloat measuredWidth = layout.usedRect.size.width;
  CGFloat measuredHeight = layout.usedRect.size.height;

  if (!CGRectIsEmpty(layout.extraLineFragmentRect)) {
    measuredHeight -= layout.extraLineFragmentRect.size.height;
  }

  // Code block's bottom padding is a spacer \n with minimumLineHeight = codeBlockPadding.
  // The layout manager may not size it accurately, so add the padding explicitly.
  if (isLastElementCodeBlock(text)) {
    measuredHeight += [_config codeBlockPadding];
  }

  if (_allowTrailingMargin && _lastElementMarginBottom > 0) {
    measuredHeight += _lastElementMarginBottom;
  }

  // Round to pixel boundaries to match React Native's <Text> measurement
  CGFloat scale = RCTScreenScale();
  return CGSizeMake(ceil(measuredWidth * scale) / scale, ceil(measuredHeight * scale) / scale);
}

- (BOOL)hasRenderedMarkdown:(NSString *)markdown
{
  return _renderedMarkdown != nil && [_renderedMarkdown isEqualToString:markdown];
}

- (void)updateState:(const facebook::react::State::Shared &)state
           oldState:(const facebook::react::State::Shared &)oldState
{
  _state = std::static_pointer_cast<const EnrichedMarkdownTextShadowNode::ConcreteState>(state);

  if (oldState == nullptr) {
    [self requestHeightUpdate];
  }
}

- (void)requestHeightUpdate
{
  if (_state == nullptr) {
    return;
  }

  _heightUpdateCounter++;
  auto selfRef = wrapManagedObjectWeakly(self);
  _state->updateState(EnrichedMarkdownTextState(_heightUpdateCounter, selfRef));
}

- (void)handleImageAttachmentDidLoad:(NSNotification *)notification
{
  if (![NSThread isMainThread]) {
    dispatch_async(dispatch_get_main_queue(), ^{ [self handleImageAttachmentDidLoad:notification]; });
    return;
  }

  if (_cachedMarkdown && _cachedMarkdown.length > 0) {
    const auto &props = *std::static_pointer_cast<EnrichedMarkdownTextProps const>(_props);
    if (!props.markdown.empty()) {
      MeasurementCache::shared().invalidateForMarkdown(props.markdown);
    }
    [self renderMarkdownContent:_cachedMarkdown];
  }
}

- (instancetype)initWithFrame:(CGRect)frame
{
  if (self = [super initWithFrame:frame]) {
    static const auto defaultProps = std::make_shared<const EnrichedMarkdownTextProps>();
    _props = defaultProps;

    self.backgroundColor = [RCTUIColor clearColor];
    _parser = [[ENRMMarkdownParser alloc] init];
    _md4cFlags = [ENRMMd4cFlags defaultFlags];

    _renderQueue = dispatch_queue_create("com.swmansion.enriched.markdown.render", DISPATCH_QUEUE_SERIAL);
    _currentRenderId = 0;

    _maxFontSizeMultiplier = 0;
    _allowTrailingMargin = NO;
    _enableLinkPreview = YES;

    _fontScaleObserver = [[FontScaleObserver alloc] init];
    __weak EnrichedMarkdownText *weakSelf = self;
    _fontScaleObserver.onChange = ^{
      EnrichedMarkdownText *strongSelf = weakSelf;
      if (!strongSelf)
        return;
      if (strongSelf->_config != nil) {
        [strongSelf->_config setFontScaleMultiplier:strongSelf->_fontScaleObserver.effectiveFontScale];
      }
      if (strongSelf->_cachedMarkdown != nil && strongSelf->_cachedMarkdown.length > 0) {
        [strongSelf renderMarkdownContent:strongSelf->_cachedMarkdown];
      }
    };

    [self setupTextView];

    [[NSNotificationCenter defaultCenter] addObserver:self
                                             selector:@selector(handleImageAttachmentDidLoad:)
                                                 name:@"ENRMImageAttachmentDidLoad"
                                               object:nil];
  }

  return self;
}

- (void)setupTextView
{
#if !TARGET_OS_OSX
  _textView = [[ENRMPlatformTextView alloc] init];
  _textView.text = @"";
#else
  _textView = [[ENRMContextMenuTextView alloc] init];
  _textView.string = @"";
#endif
  ENRMConfigureMarkdownTextView(_textView);
  _textView.delegate = self;
  _textView.hidden = YES;

#if TARGET_OS_OSX
  __weak EnrichedMarkdownText *weakSelf = self;
  ((ENRMContextMenuTextView *)_textView).contextMenuProvider =
      ^NSMenu *_Nullable(NSMenu *baseMenu, NSTextView *textView)
  {
    EnrichedMarkdownText *strongSelf = weakSelf;
    if (!strongSelf) {
      return baseMenu;
    }
    return buildEditMenuForSelection(textView.textStorage, textView.selectedRange, strongSelf->_cachedMarkdown,
                                     strongSelf->_config, @[ baseMenu ]);
  };

  ((ENRMContextMenuTextView *)_textView).linkClickHandler = ^BOOL(NSString *url) {
    EnrichedMarkdownText *strongSelf = weakSelf;
    if (!strongSelf)
      return NO;

    // Handle anchor links by scrolling natively
    if ([url hasPrefix:@"#"]) {
      [strongSelf scrollToAnchor:url];
    }

    // Emit to JS
    auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(strongSelf->_eventEmitter);
    if (eventEmitter) {
      eventEmitter->onLinkPress({.url = std::string([url UTF8String])});
    }

    return YES; // We handled it
  };
#endif

  ENRMTapRecognizer *tapRecognizer = [[ENRMTapRecognizer alloc] initWithTarget:self action:@selector(textTapped:)];
  [_textView addGestureRecognizer:tapRecognizer];

#if TARGET_OS_OSX
  // Wrap text view in NSScrollView for native macOS scrolling.
  // Fabric's layout system doesn't re-measure after YGNodeMarkDirty on macOS,
  // so the component stays viewport-sized. The NSScrollView handles overflow.
  _scrollContainer = [[NSScrollView alloc] initWithFrame:self.bounds];
  _scrollContainer.hasVerticalScroller = YES;
  _scrollContainer.hasHorizontalScroller = NO;
  _scrollContainer.drawsBackground = NO;
  _scrollContainer.backgroundColor = [NSColor clearColor];
  _scrollContainer.contentView.drawsBackground = NO;
  _scrollContainer.documentView = _textView;
  self.contentView = _scrollContainer;
#else
  self.contentView = _textView;
#endif
}

- (void)didAddSubview:(RCTUIView *)subview
{
  [super didAddSubview:subview];

  if (subview == _textView) {
    [self setupLayoutManager];
  }
}

- (void)willRemoveSubview:(RCTUIView *)subview
{
  if (subview == _textView && _textView.layoutManager != nil) {
    NSLayoutManager *layoutManager = _textView.layoutManager;
    if ([object_getClass(layoutManager) isEqual:[TextViewLayoutManager class]]) {
      [layoutManager setValue:nil forKey:@"config"];
      object_setClass(layoutManager, [NSLayoutManager class]);
    }
  }
  [super willRemoveSubview:subview];
}

- (void)setupLayoutManager
{
  // Custom layout manager handles drawing for code blocks, blockquotes, etc.
  NSLayoutManager *layoutManager = _textView.layoutManager;
  if (layoutManager != nil) {
    layoutManager.allowsNonContiguousLayout = NO; // workaround for onScroll issue
    object_setClass(layoutManager, [TextViewLayoutManager class]);

    if (_config != nil) {
      [layoutManager setValue:_config forKey:@"config"];
    }
  }
}

- (void)renderMarkdownContent:(NSString *)markdownString
{
  if (_blockAsyncRender) {
    return;
  }

  _cachedMarkdown = [markdownString copy];

  NSUInteger renderId = ++_currentRenderId;

  StyleConfig *config = [_config copy];
  ENRMMarkdownParser *parser = _parser;
  ENRMMd4cFlags *md4cFlags = [_md4cFlags copy];

  BOOL allowFontScaling = _fontScaleObserver.allowFontScaling;
  CGFloat maxFontSizeMultiplier = _maxFontSizeMultiplier;
  BOOL allowTrailingMargin = _allowTrailingMargin;

  NSWritingDirection writingDirection = currentWritingDirection();

  dispatch_async(_renderQueue, ^{
    MarkdownASTNode *ast = [parser parseMarkdown:markdownString flags:md4cFlags];
    if (!ast) {
      return;
    }

    AttributedRenderer *renderer = [[AttributedRenderer alloc] initWithConfig:config];
    [renderer setAllowTrailingMargin:allowTrailingMargin];
    RenderContext *context = [RenderContext new];
    context.allowFontScaling = allowFontScaling;
    context.maxFontSizeMultiplier = maxFontSizeMultiplier;
    context.writingDirection = writingDirection;
    NSMutableAttributedString *attributedText = [renderer renderRoot:ast context:context];
    applyInlineHTMLPostProcessing(attributedText);

    CGFloat lastElementMarginBottom = [renderer getLastElementMarginBottom];

    [context applyLinkAttributesToString:attributedText];

    AccessibilityInfo *accessibilityInfo = [AccessibilityInfo infoFromContext:context];

    dispatch_async(dispatch_get_main_queue(), ^{
      if (renderId != self->_currentRenderId) {
        return;
      }

      self->_lastElementMarginBottom = lastElementMarginBottom;
      self->_accessibilityInfo = accessibilityInfo;

      [self applyRenderedText:attributedText];
    });
  });
}

- (NSMutableAttributedString *)parseAndRenderMarkdown:(NSString *)markdownString
{
  MarkdownASTNode *ast = [_parser parseMarkdown:markdownString flags:_md4cFlags];
  if (!ast) {
    return nil;
  }

  AttributedRenderer *renderer = [[AttributedRenderer alloc] initWithConfig:_config];
  [renderer setAllowTrailingMargin:_allowTrailingMargin];
  RenderContext *context = [RenderContext new];
  context.allowFontScaling = _fontScaleObserver.allowFontScaling;
  context.maxFontSizeMultiplier = _maxFontSizeMultiplier;
  context.writingDirection = currentWritingDirection();
  NSMutableAttributedString *attributedText = [renderer renderRoot:ast context:context];
  applyInlineHTMLPostProcessing(attributedText);

  _lastElementMarginBottom = [renderer getLastElementMarginBottom];

  [context applyLinkAttributesToString:attributedText];

  _accessibilityInfo = [AccessibilityInfo infoFromContext:context];

  return attributedText;
}

/// Synchronous rendering for mock view measurement (no UI updates needed).
- (void)renderMarkdownSynchronously:(NSString *)markdownString
{
  if (!markdownString || markdownString.length == 0) {
    return;
  }

  _blockAsyncRender = YES;
  _cachedMarkdown = [markdownString copy];

  NSMutableAttributedString *attributedText = [self parseAndRenderMarkdown:markdownString];
  if (!attributedText) {
    return;
  }

  _textView.attributedText = attributedText;
  _renderedMarkdown = [_cachedMarkdown copy];
}

- (void)applyRenderedText:(NSMutableAttributedString *)attributedText
{
  NSUInteger tailStart = _previousTextLength;

  NSLayoutManager *layoutManager = _textView.layoutManager;
  if ([layoutManager isKindOfClass:[TextViewLayoutManager class]]) {
    [layoutManager setValue:_config forKey:@"config"];
  }

  objc_setAssociatedObject(_textView.textContainer, kTextViewKey, _textView, OBJC_ASSOCIATION_ASSIGN);

  // Ensure the text container has unlimited height before setting content.
  // updateLayoutMetrics may have shrunk the frame (and thus the text container)
  // from a previous layout pass, which would clip the new attributed text.
  CGFloat containerWidth = _textView.textContainer.size.width;
  if (containerWidth <= 0) {
    containerWidth = self.bounds.size.width;
  }
  _textView.textContainer.size = CGSizeMake(containerWidth, CGFLOAT_MAX);

  _textView.attributedText = attributedText;
  _renderedMarkdown = [_cachedMarkdown copy];

  [_textView.layoutManager invalidateLayoutForCharacterRange:NSMakeRange(0, attributedText.length)
                                        actualCharacterRange:NULL];

  // When bounds width is zero (recycled view not yet laid out), skip layout
  // and measurement — didMoveToWindow will handle it once the view has real
  // bounds. Measuring with width=0 produces a bogus single-line measurement
  // that corrupts the height sent to Yoga.
  if (self.bounds.size.width > 0) {
    [_textView.layoutManager ensureLayoutForTextContainer:_textView.textContainer];
    ENRMSetNeedsDisplay(_textView);
#if !TARGET_OS_OSX
    [self setNeedsLayout];
#endif

    CGSize measured = [self measureSize:self.bounds.size.width];
    if (needsHeightUpdate(measured, self.bounds)) {
      [self requestHeightUpdate];
    }
  }

  _accessibilityNeedsRebuild = YES;

  if (_textView.hidden) {
    dispatch_async(dispatch_get_main_queue(), ^{ self->_textView.hidden = NO; });
  }

  if (_streamingAnimation) {
    if (!_fadeAnimator) {
      _fadeAnimator = [[ENRMTailFadeInAnimator alloc] initWithTextView:_textView];
    }
    [_fadeAnimator animateFrom:tailStart to:attributedText.length];
    _previousTextLength = attributedText.length;
  }
}

- (void)updateProps:(Props::Shared const &)props oldProps:(Props::Shared const &)oldProps
{
  const auto &oldViewProps = *std::static_pointer_cast<EnrichedMarkdownTextProps const>(_props);
  const auto &newViewProps = *std::static_pointer_cast<EnrichedMarkdownTextProps const>(props);

  BOOL stylePropChanged = NO;

  if (_config == nil) {
    _config = [[StyleConfig alloc] init];
    [_config setFontScaleMultiplier:_fontScaleObserver.effectiveFontScale];
  }

  stylePropChanged = applyMarkdownStyleToConfig(_config, newViewProps.markdownStyle, oldViewProps.markdownStyle);

  if (stylePropChanged) {
    [ENRMImageAttachment clearAttachmentRegistry];
  }

  NSLayoutManager *layoutManager = _textView.layoutManager;
  if ([layoutManager isKindOfClass:[TextViewLayoutManager class]]) {
    StyleConfig *currentConfig = [layoutManager valueForKey:@"config"];
    if (currentConfig != _config) {
      [layoutManager setValue:_config forKey:@"config"];
    }
  }

  if (_textView.selectable != newViewProps.selectable) {
    _textView.selectable = newViewProps.selectable;
  }

  if (newViewProps.allowFontScaling != oldViewProps.allowFontScaling) {
    _fontScaleObserver.allowFontScaling = newViewProps.allowFontScaling;

    if (_config != nil) {
      [_config setFontScaleMultiplier:_fontScaleObserver.effectiveFontScale];
    }

    stylePropChanged = YES;
  }

  if (newViewProps.maxFontSizeMultiplier != oldViewProps.maxFontSizeMultiplier) {
    _maxFontSizeMultiplier = newViewProps.maxFontSizeMultiplier;

    if (_config != nil) {
      [_config setMaxFontSizeMultiplier:_maxFontSizeMultiplier];
    }

    stylePropChanged = YES;
  }

  if (newViewProps.allowTrailingMargin != oldViewProps.allowTrailingMargin) {
    _allowTrailingMargin = newViewProps.allowTrailingMargin;
  }

  BOOL md4cFlagsChanged = NO;
  if (newViewProps.md4cFlags.underline != oldViewProps.md4cFlags.underline) {
    _md4cFlags.underline = newViewProps.md4cFlags.underline;
    md4cFlagsChanged = YES;
  }
  if (newViewProps.md4cFlags.latexMath != oldViewProps.md4cFlags.latexMath) {
    _md4cFlags.latexMath = newViewProps.md4cFlags.latexMath;
    md4cFlagsChanged = YES;
  }

  BOOL markdownChanged = oldViewProps.markdown != newViewProps.markdown;
  BOOL allowTrailingMarginChanged = newViewProps.allowTrailingMargin != oldViewProps.allowTrailingMargin;

  _enableLinkPreview = newViewProps.enableLinkPreview;

  if (newViewProps.streamingAnimation != oldViewProps.streamingAnimation) {
    _streamingAnimation = newViewProps.streamingAnimation;
    if (_streamingAnimation) {
      _previousTextLength = ENRMGetAttributedText(_textView).length;
    } else {
      [_fadeAnimator cancel];
      _fadeAnimator = nil;
      _previousTextLength = 0;
    }
  }

  if (markdownChanged || stylePropChanged || md4cFlagsChanged || allowTrailingMarginChanged) {
    NSString *markdownString = [[NSString alloc] initWithUTF8String:newViewProps.markdown.c_str()];
    [self renderMarkdownContent:markdownString];
  }

  [super updateProps:props oldProps:oldProps];
}

- (void)didMoveToWindow
{
  [super didMoveToWindow];

  if (self.window && _renderedMarkdown != nil) {
    _textView.hidden = NO;
    ENRMRefreshTextViewAfterWindowAttach(_textView, self.bounds);

    CGSize measured = [self measureSize:self.bounds.size.width];
    if (needsHeightUpdate(measured, self.bounds)) {
      [self requestHeightUpdate];
    }
  }
}

Class<RCTComponentViewProtocol> EnrichedMarkdownTextCls(void)
{
  return EnrichedMarkdownText.class;
}

- (facebook::react::SharedTouchEventEmitter)touchEventEmitterAtPoint:(CGPoint)point
{
  if (_textView) {
    CGPoint textViewPoint = [self convertPoint:point toView:_textView];
    if (isPointOnInteractiveElement(_textView, textViewPoint)) {
      return nil;
    }
  }

  return [super touchEventEmitterAtPoint:point];
}

/// Convert heading text to a GitHub-style anchor slug:
/// lowercase, strip non-alphanumeric (except spaces/hyphens), spaces→hyphens
static NSString *slugifyHeading(NSString *headingText)
{
  NSString *lower = [headingText lowercaseString];
  NSMutableString *slug = [NSMutableString string];
  for (NSUInteger i = 0; i < lower.length; i++) {
    unichar c = [lower characterAtIndex:i];
    if ((c >= 'a' && c <= 'z') || (c >= '0' && c <= '9') || c == '-' || c == '_') {
      [slug appendFormat:@"%C", c];
    } else if (c == ' ') {
      [slug appendString:@"-"];
    }
    // strip everything else
  }
  return slug;
}

- (BOOL)scrollToAnchor:(NSString *)fragment
{
  if (!_textView)
    return NO;

  // Strip leading '#'
  NSString *anchor = fragment;
  if ([anchor hasPrefix:@"#"]) {
    anchor = [anchor substringFromIndex:1];
  }
  if (anchor.length == 0)
    return NO;

  NSAttributedString *attrText = ENRMGetAttributedText(_textView);
  if (!attrText || attrText.length == 0)
    return NO;

  __block NSRange matchRange = NSMakeRange(NSNotFound, 0);

  [attrText enumerateAttribute:MarkdownTypeAttributeName
                       inRange:NSMakeRange(0, attrText.length)
                       options:0
                    usingBlock:^(id value, NSRange range, BOOL *stop) {
                      if (![value isKindOfClass:[NSString class]])
                        return;
                      NSString *type = (NSString *)value;
                      if (![type hasPrefix:@"heading-"])
                        return;

                      NSString *headingText = [[attrText attributedSubstringFromRange:range] string];
                      headingText = [headingText
                          stringByTrimmingCharactersInSet:[NSCharacterSet whitespaceAndNewlineCharacterSet]];
                      NSString *slug = slugifyHeading(headingText);

                      if ([slug isEqualToString:anchor]) {
                        matchRange = range;
                        *stop = YES;
                      }
                    }];

  if (matchRange.location == NSNotFound)
    return NO;

  // Get the glyph rect for the heading
  NSLayoutManager *layoutManager = _textView.layoutManager;
  NSTextContainer *textContainer = _textView.textContainer;
  NSRange glyphRange = [layoutManager glyphRangeForCharacterRange:matchRange actualCharacterRange:NULL];
  CGRect headingRect = [layoutManager boundingRectForGlyphRange:glyphRange inTextContainer:textContainer];

#if TARGET_OS_OSX
  if (!_scrollContainer)
    return NO;

  // Add text container inset offset
  NSEdgeInsets inset = _textView.textContainerInset;
  headingRect.origin.x += inset.left;
  headingRect.origin.y += inset.top;

  [_scrollContainer.contentView scrollToPoint:NSMakePoint(0, headingRect.origin.y)];
  [_scrollContainer reflectScrolledClipView:_scrollContainer.contentView];
#else
  // On iOS, scrollEnabled is NO on the UITextView — walk up the view
  // hierarchy to find the parent UIScrollView (typically a React Native
  // ScrollView) and scroll it instead.
  UIEdgeInsets inset = _textView.textContainerInset;
  CGFloat targetY = headingRect.origin.y + inset.top;

  // Convert from text view coordinates to the scroll view's coordinate space
  UIScrollView *parentScroll = nil;
  UIView *current = _textView.superview;
  while (current) {
    if ([current isKindOfClass:[UIScrollView class]]) {
      parentScroll = (UIScrollView *)current;
      break;
    }
    current = current.superview;
  }
  if (!parentScroll)
    return NO;

  CGPoint pointInScroll = [_textView convertPoint:CGPointMake(0, targetY) toView:parentScroll];
  CGFloat maxOffsetY = parentScroll.contentSize.height - parentScroll.bounds.size.height;
  CGFloat clampedY = MIN(pointInScroll.y, MAX(maxOffsetY, 0));
  [parentScroll setContentOffset:CGPointMake(0, clampedY) animated:YES];
#endif

  return YES;
}

- (void)textTapped:(ENRMTapRecognizer *)recognizer
{
  ENRMPlatformTextView *textView = (ENRMPlatformTextView *)recognizer.view;

  if (handleTaskListTapWithSharedLogic(
          textView, recognizer, &self->_cachedMarkdown, self->_config,
          ^(NSInteger index, BOOL checked, NSString *itemText) {
            auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(self->_eventEmitter);
            if (eventEmitter) {
              eventEmitter->onTaskListItemPress({
                  .index = (int)index,
                  .checked = checked,
                  .text = std::string([itemText UTF8String] ?: ""),
              });
            }
          },
          ^(NSString *updatedMarkdown) { [self renderMarkdownContent:updatedMarkdown]; })) {
    return;
  }

  NSString *url = linkURLAtTapLocation(textView, recognizer);
  if (url) {
    // Handle in-document anchor links natively by scrolling to the heading
    if ([url hasPrefix:@"#"] && [self scrollToAnchor:url]) {
      // Still emit to JS so the consuming app can track/log it
      auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(_eventEmitter);
      if (eventEmitter) {
        eventEmitter->onLinkPress({.url = std::string([url UTF8String])});
      }
      return;
    }
    auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(_eventEmitter);
    if (eventEmitter) {
      eventEmitter->onLinkPress({.url = std::string([url UTF8String])});
    }
    return;
  }

  ENRMClearSelection(textView);
}

#pragma mark - UITextViewDelegate (Link Interaction)

#if !TARGET_OS_OSX
- (BOOL)textView:(ENRMPlatformTextView *)textView
    shouldInteractWithURL:(NSURL *)URL
                  inRange:(NSRange)characterRange
              interaction:(UITextItemInteraction)interaction
{
  // Intercept anchor links and scroll to the heading instead of opening
  NSString *urlStr = URL.absoluteString;
  NSString *fragment = URL.fragment;
  if (fragment.length > 0 || [urlStr hasPrefix:@"#"]) {
    NSString *anchor = fragment.length > 0 ? [@"#" stringByAppendingString:fragment] : urlStr;
    if ([self scrollToAnchor:anchor]) {
      auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(_eventEmitter);
      if (eventEmitter) {
        eventEmitter->onLinkPress({.url = std::string([anchor UTF8String])});
      }
      return NO;
    }
  }

  if (interaction != UITextItemInteractionPresentActions) {
    return YES;
  }

  NSString *urlString = linkURLAtRange(textView, characterRange);

  if (!urlString || _enableLinkPreview) {
    return YES;
  }

  auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(_eventEmitter);
  if (eventEmitter) {
    eventEmitter->onLinkLongPress({.url = std::string([urlString UTF8String])});
  }
  return NO;
}

#pragma mark - UITextViewDelegate (Edit Menu)

- (UIMenu *)textView:(ENRMPlatformTextView *)textView
    editMenuForTextInRange:(NSRange)range
          suggestedActions:(NSArray<UIMenuElement *> *)suggestedActions API_AVAILABLE(ios(16.0))
{
  return buildEditMenuForSelection(textView.attributedText, range, _cachedMarkdown, _config, suggestedActions);
}
#endif

#if TARGET_OS_OSX
#pragma mark - NSTextViewDelegate (Link Clicks — macOS)

- (BOOL)textView:(NSTextView *)textView clickedOnLink:(id)link atIndex:(NSUInteger)charIndex
{
  // link is either an NSString or NSURL depending on how NSLinkAttributeName was set
  NSString *urlString = nil;
  if ([link isKindOfClass:[NSURL class]]) {
    urlString = [(NSURL *)link absoluteString];
  } else if ([link isKindOfClass:[NSString class]]) {
    urlString = (NSString *)link;
  }

  if (!urlString)
    return NO;

  // Check custom "linkURL" attribute first (may differ from NSLinkAttributeName value)
  NSAttributedString *attrText = ENRMGetAttributedText(_textView);
  if (charIndex < attrText.length) {
    NSString *customURL = [attrText attribute:@"linkURL" atIndex:charIndex effectiveRange:NULL];
    if (customURL)
      urlString = customURL;
  }

  // Handle anchor links by scrolling natively
  if ([urlString hasPrefix:@"#"]) {
    [self scrollToAnchor:urlString];
  }

  // Emit to JS
  auto eventEmitter = std::static_pointer_cast<EnrichedMarkdownTextEventEmitter const>(_eventEmitter);
  if (eventEmitter) {
    eventEmitter->onLinkPress({.url = std::string([urlString UTF8String])});
  }

  return YES; // We handled it — don't let NSTextView open it in browser
}
#endif

#pragma mark - Accessibility (VoiceOver Navigation)

- (void)rebuildAccessibilityElementsIfNeeded
{
  if (!_accessibilityNeedsRebuild) {
    return;
  }
  _accessibilityNeedsRebuild = NO;
#if !TARGET_OS_OSX
  _accessibilityElements = [MarkdownAccessibilityElementBuilder buildElementsForTextView:_textView
                                                                                    info:_accessibilityInfo
                                                                               container:self];
#else
  _accessibilityElements = [NSMutableArray array];
#endif
}

- (BOOL)isAccessibilityElement
{
  return NO;
}

- (NSInteger)accessibilityElementCount
{
  [self rebuildAccessibilityElementsIfNeeded];
  return _accessibilityElements.count;
}

- (id)accessibilityElementAtIndex:(NSInteger)index
{
  [self rebuildAccessibilityElementsIfNeeded];
  if (index < 0 || index >= (NSInteger)_accessibilityElements.count) {
    return nil;
  }
  return _accessibilityElements[index];
}

- (NSInteger)indexOfAccessibilityElement:(id)element
{
  [self rebuildAccessibilityElementsIfNeeded];
  return [_accessibilityElements indexOfObject:element];
}

- (NSArray *)accessibilityElements
{
  [self rebuildAccessibilityElementsIfNeeded];
  return _accessibilityElements;
}

#if !TARGET_OS_OSX
- (NSArray<UIAccessibilityCustomRotor *> *)accessibilityCustomRotors
{
  [self rebuildAccessibilityElementsIfNeeded];
  return [MarkdownAccessibilityElementBuilder buildRotorsFromElements:_accessibilityElements];
}
#endif

@end
